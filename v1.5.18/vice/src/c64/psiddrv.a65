; 
; psiddrv.a65 - PSID player.
;
; Written by
;  Dag Lem  <resid@nimrod.no>
;
; This file is part of VICE, the Versatile Commodore Emulator.
; See README for copyright notice.
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA
;  02111-1307  USA.
;

	*=$0300

	; 6510 vectors
nmivec	.word nmi
coldvec	.word cold
irqvec	.word irq

	; parameters
initvec	.word 0
playvec	.word 0
playmax	.byte 0
playnum	.byte 0
speed	.word 0, 0
volume	.byte 0
portval	.byte 0

retusr	.word irqret
	
	; user interrupt vectors
	; *=$0314
irqusr	.word irqjob
brkusr	.word brkjob
nmiusr	.word nmijob

	; cold start
brkjob
cold	sei
	ldx #$ff
	txs
	cld
	; turn interrupts off
	lda #$00
	sta $d01a
	lda $d019
	sta $d019
	lda #$7f
	sta $dc0d
	sta $dd0d
	lda $dc0d
	lda $dd0d

	; set VICII raster to line 0
	lda #$1b
	ldx #$00
	sta $d011
	stx $d012

	; set CIA Timer A to 60Hz
	lda $02a6
	beq ntsc
pal	lda #$25
	ldx #$40
	bne timer
ntsc	lda #$95
	ldx #$42
timer	sta $dc04
	stx $dc05

	; enable CIA 1 timer A interrupt
ciainit	lda #$81
	ldx #$01
	sta $dc0d
	stx $dc0e

	; init psid
	lda #$2f
	sta $00
	lda initvec+1
	cmp #$e0
	bcc kernal
	lda #$35
	bne set01
kernal	cmp #$c0
	bcs basic
	cmp #$a0
	bcc basic
	lda #$36
	bne set01
basic	lda #$37
set01	sta $01
	
	lda volume
	sta $d418

	; get song number
	ldy playnum
	dey
	cpy playmax
	bcc songset
	ldy #$00
songset	tya
	pha

	lda playvec+1
	beq doinit

	; get shift number for speed bit
	cpy #32
	bcc shftset
	ldy #31

	; find speed for current song number
shftset	lda #$00
	tax
	sec
shift	rol
	bcc nxtshft
	inx
	rol
nxtshft	dey
	bpl shift
	and speed,x
	bne doinit
	
	; disable CIA1 timer A interrupt, enable VICII raster interrupt
	lda #$7f
	sta $dc0d
	lda #$81
	sta $d01a

	; call song init routine
doinit	pla
	jsr init

run	cli
idle	jmp idle

	; Init/play PSID
init	jmp (initvec)
play	jmp (playvec)

	; IRQ handler
irq	pha
	txa
	pha
	tya
	pha
	tsx
	lda $0104,x
	and #$10
	beq irqjmp
	jmp (brkusr)
irqjmp	jmp (irqusr)
irqjob	lda playvec+1
	beq noplay
	jsr play
noplay	lda $d019
	sta $d019
irqret	lda $dc0d
	pla
	tay
	pla
	tax
	pla
	rti

	; NMI handler
nmi	sei
	jmp (nmiusr)
nmijob	rti
